#################################################################################
#                                                                               #
# Sequential inference: ancestral state reconstruction using stochastic mapping #
#                                                                               #
# author: Priscilla Lau                                                        #
#                                                                               #
#################################################################################

#######################
# Reading in the Data #
#######################

### Select the character to analyze
characterDisc <- 1

### Read in the tree
T <- readTrees("data/artiodactyla.tree")[1]
ntips <- T.ntips()
nbranches <- 2 * ntips - 2

disc <- readDiscreteCharacterData("data/artiodactyla_Discrete.nex")
num_disc_states <- disc.getStateDescriptions().size()
disc.excludeAll()
disc.includeCharacter(characterDisc)


# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()
avmvn = mvAVMVN(weight=20, waitBeforeLearning=500, waitBeforeUsing=1000)

##########################
# Specify the tree model #
##########################

tree <- T
root_age <- tree.rootAge()

########################################
# Specify the discrete-character model #
########################################

# make the Q matrix

rates ~ dnDirichlet(rep(1,6)*10)
moves.append( mvBetaSimplex( rates, weight=2 ) )
moves.append( mvDirichletSimplex( rates, weight=2 ) )
avmvn.addVariable( rates )



#lambda ~ dnLognormal(0,1)
lambda ~ dnLognormal(ln(0.05), 0.4)
moves.append( mvScale(lambda, weight=1.0) )
avmvn.addVariable(lambda)


Q := fnFreeK(lambda*rates, rescaled=FALSE)

rf_prior <- rep(1,num_disc_states)
rf ~ dnDirichlet( rf_prior )
moves.append( mvBetaSimplex( rf, weight=2 ) )
moves.append( mvDirichletSimplex( rf, weight=2 ) )
avmvn.addVariable( rf )
moves.append( avmvn )

# make the CTCM model
X ~ dnPhyloCTMC(tree=tree, Q=Q, rootFrequencies=rf, type="Standard")
X.clamp(disc)

#############
# The Model #
#############

mymodel = model(X)

monitors.append( mnModel(filename="output/ase/trace.log", printgen=10) )
monitors.append( mnScreen(printgen=1000) )
monitors.append( mnJointConditionalAncestralState(tree=tree,
                                                  ctmc=X,
                                                  filename="output/ase/states.log",
                                                  type="Standard",
                                                  printgen=10,
                                                  withTips=true,
                                                  withStartStates=false) )

monitors.append( mnStochasticCharacterMap(ctmc=X,
                                          filename="output/ase/charhist.log",
                                          printgen=10,
                                          include_simmap=true) )




################
# The Analysis #
################

### workspace mcmc ###
mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")


### run the MCMC ###
mymcmc.burnin(generations=500, tuningInterval=200)
mymcmc.run(generations=5000, tuningInterval=200)

anc_states = readAncestralStateTrace("output/ase/states.log")
anc_tree = ancestralStateTree(tree=tree, ancestral_state_trace_vector=anc_states, include_start_states=false, file="output/ase/anc_states.tre", burnin=0.0, summary_statistic="MAP", site=1, nStates=3)
anc_states_stoch_map = readAncestralStateTrace("output/ase/charhist.log")
char_map_tree = characterMapTree(tree=tree,
                 ancestral_state_trace_vector=anc_states_stoch_map,
                 character_file="output/ase/marginal_character.tre",
                 posterior_file="output/ase/marginal_posterior.tre",
                 burnin=0.0,
                 num_time_slices=500)


## quit ##
q()
