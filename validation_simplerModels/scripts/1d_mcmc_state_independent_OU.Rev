################################################################################
#
# RevBayes Example: Bayesian inference of rates of evolution under a
#                   state-dependent Brownian-motion model
#
#
# authors: Sebastian Hoehna
#
################################################################################

#######################
# Reading in the Data #
#######################

### Select the character to analyze
characterCont <- 1

### Read in the tree
tree <- readTrees("data/artiodactyla.tree")[1]
ntips <- tree.ntips()
nbranches <- 2 * ntips - 2
root_age <- tree.rootAge()

### Read in the continuous-character data
cont <- readContinuousCharacterData("data/artiodactyla_Continuous.nex")
cont.excludeAll()
cont.includeCharacter(characterCont)

# Create some vector for the moves and monitors of this analysis
moves    = VectorMoves()
monitors = VectorMonitors()
avmvn_cont = mvAVMVN(weight=20, waitBeforeLearning=500, waitBeforeUsing=1000)


##########################
# Specify the rate model #
##########################

alpha <- 0.0

theta ~ dnUniform(3,7)
moves.append(mvSlide(theta, weight = 1.0) )
avmvn_cont.addVariable(theta)

vy <- cont.var(characterCont)
sigma2 ~ dnLognormal(ln(vy), 0.587405)
moves.append(mvScale(sigma2, weight = 1.0) )
avmvn_cont.addVariable(sigma2)

moves.append( avmvn_cont )


##########################
# Specify the OU process #
##########################

Y ~ dnPhyloOrnsteinUhlenbeckPruning(tree, sigma=sigma2^0.5, alpha=alpha, theta=theta, rootStates=theta)
Y.clamp(cont)

#############
# The Model #
#############

mymodel = model(Y)

### set up the monitors that will output parameter values to file and screen
monitors.append( mnModel(filename="output/1d_state_independent_OU.log", printgen=10) )
#monitors.append( mnScreen(printgen=1000, sigma2) )


################
# The Analysis #
################

### workspace mcmc ###
mymcmc = mcmc(mymodel, monitors, moves, nruns=2, combine="mixed")


### run the MCMC ###
mymcmc.burnin(generations=5000, tuningInterval=200)
mymcmc.run(generations=100000, tuningInterval=200)


## quit ##
q()
